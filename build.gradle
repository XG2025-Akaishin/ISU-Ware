plugins {
	id 'java'
	id "fabric-loom" version "1.8-SNAPSHOT"
	id 'com.gradleup.shadow' version '9.0.0-beta4'
	id 'io.freefair.lombok' version '8.11'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.mod_id
}

repositories {
	maven {
		name = "meteor-maven"
		url = "https://maven.meteordev.org/releases"
	}
	maven {
		name = "meteor-maven"
		url = "https://maven.meteordev.org"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

	implementation(shadow("org.reflections:reflections:0.10.2"))
	implementation(shadow("meteordevelopment:discord-ipc:1.1"))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

loom {
	accessWidenerPath = file("src/main/resources/sydney.accesswidener")
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveClassifier.set("dev")
}

remapJar {
	dependsOn(shadowJar)
	inputFile = tasks.shadowJar.archiveFile
}